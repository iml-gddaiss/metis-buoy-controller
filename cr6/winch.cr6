'Winch related code

Const Min_Air_Temp_for_Winch = 1
Const Max_Wave_H13_for_Winch = 2
Const Min_Wave_Period_for_Winch = 6
Const Max_Flow_for_Winch = 1
Const Max_Buoy_Speed_for_Winch = 1
Const Min_Voltage_for_Winch = 12.2

Public OnWhichBattery ' On?Winch?Battery Fixme
Public Winch_Voltage As Float = ""

Public WinchStatus as String * 10 'Standby / Sampling / Completed
Public WinchMessage As String * 50
Public WinchMissingCondition As Long

Public WinchString As String * 50
Public WinchFileName As String * 50
Dim WinchFile As Long


Public Winch_Last_Sample AS Long = ""
Public Winch_DateTime(2) As String : Alias Winch_DateTime = Winch_Date, Winch_Time  
Public Winch_Latitude As String
Public Winch_Longitude As String

' ####################### Start Conditions ############################## 
Sub WinchAirCheck(Air_Temp)
    If Air_Temp < Min_Air_Temp_for_Winch Then
        WinchMissingCondition += 1 ' 2^0
        WinchMessage = "The air temperature is too cold"
    EndIf
EndSub

Sub WinchWaveCheck(Wave_H13, Wave_Period)
    If Wave_H13 > Max_Wave_H13_for_Winch Then  ' FIXME Wave_H13 is a String
        WinchMissingCondition += 2 ' 2^1
        WinchMessage = "Average wave is too high"
    EndIf
        
    If Wave_H13 > 1 AND Wave_Period < Min_Wave_Period_for_Winch  AND Wave_Period <> 0 Then ' FIXME Wave_H13 are WavePeriod are Strings
        WinchMissingCondition += 4 ' 2^2
        WinchMessage = "Wave period is too short"
    EndIf
EndSub

Sub WinchGPSCheck(SOG)
    If SOG > Max_Buoy_Speed_for_Winch Then
        WinchMissingCondition += 8 ' 2^3
        WinchMessage = "The buoy is moving too fast"
    EndIf
EndSub

Sub WinchPowerCheck(VBatt1, VBatt2)
    OnWhichBattery = Mid(Power(11),4,1)
    If OnWhichBattery = 1 Then
        Winch_Voltage = VBatt1
    ElseIf OnWhichBattery = 2 Then
        Winch_Voltage = VBatt2
    EndIf
    
    If Winch_Voltage < Min_Voltage_for_Winch Then
        WinchMissingCondition += 16 ' 2^4
        WinchMessage = "The Winch voltage is too low"
    EndIf
EndSub

Sub WinchConditionsCheck
    WinchMissingCondition = 0
    
    If With_WXT536 Then
      Call WinchAirCheck(Air_Temp)
    EndIf
    
    If With_Wave Then
      Call WinchWaveCheck(Wave_H13, Wave_Period) 
    EndIf
          
    If With_GPS Then
      Call WinchGPSCheck(SOG)
    EndIf
    
    If With_Power Then
      Call WinchPowerCheck(VBatt1, VBatt2)
    EndIf
EndSub

' ####################### Winch Mission Start ############################## 
Sub CheckWinchStatus 'Should it be started earlier ?? 'Should it be close afterward
    SerialOpen(SerialWinch,19200,3,20,100) 'Should it be started earlier ??
    SerialFlush(SerialWinch)
    Delay(1,500,mSec)
    SerialIn(WinchString,SerialWinch,100,10,60)

    If WinchString = "" Then
        If WinchStatus = "Sampling"
            WinchStatus = "Completed"
            WinchMessage = "Winch Completed"
        Else
            WinchMessage = "No Winch in Progress"
        EndIf
    Else
        WinchMessage = "Winch in Progress"
    EndIf
EndSub

Sub StartWinchMission
    SerialOpen(SerialWinch,19200,3,20,100) 'Should it be started earlier ?? 'Should it be close afterward
    SerialOut(SerialWinch,CHR(13),"",1,0)
    SerialOut(SerialWinch,"p1"&CHR(13)&CHR(10),.,2,300)
    Delay(1,2,Sec)
    SerialFlush(SerialWinch)
    SerialIn(WinchString,SerialWinch,100,10,100)
    SerialOut(SerialWinch,"p0"&CHR(13)&CHR(10),.,2,300)

    If Mid(WinchString,1,3) = "[S]" Then 'If [S] is received from CTD -> winch has started
            SerialOut(SerialWinch,"X"&CHR(13)&CHR(10),"[W]1B>Operation Complete",2,200)
            SerialOut(SerialWinch,"R"&CHR(13)&CHR(10),"[W]09>Top Switch Activated",3,1000)
            SerialOut(SerialWinch,"K"&CHR(13)&CHR(10),"[W]13>Lenght Manually Cleared",2,200)
            SerialOut(SerialWinch,"Y"&Depth_for_Winch&CHR(13)&CHR(10),CHR(13),1,0)
            
            WinchStatus = "Sampling"
            
            If Start_Winch_Override Then
                'Updating GPS Values for Winch TimeStamp
                Call StartGPS              
                Call CollectGPS(true) ' single_sample = true
                SerialClose(SerialGPS)
            Else
                ' The next winch is still scheduled isn't changed if Start_Winch_Override is True.
                Winch_Last_Sample = Public.TimeStamp(1)
            EndIf

            SplitStr(Winch_DateTime, Public.TimeStamp(4), " ", 2, 5) ' Fixme TO test
            Winch_Latitude = Latitude
            Winch_Longitude = Longitude

            WinchMessage = "Winch Mission Started"
    Else 'No string from CTD
            WinchMessage = "No String received from CTD" 'and ???? Fixme 
    EndIf
EndSub

Sub WinchMission
    'Start a Winch Mission if its not Ongoing and possible.
    If With_Winch AND (SBE37_In_Saltwater OR Bypass_Salinity) Then
               
        WhereAmI = "Entering the WinchMission"
        
        If ((Winch_Last_Sample + Winch_Interval_Hr * 3600 - 60) > Public.TimeStamp(1)) AND NOT Start_Winch_Override Then
            ' Skipped if Start_Winch_Override is True.
            ' minus 60sec to not skip a scan planned at Winch_Interval hour (got 3599s and didn't sampled)
            WinchMessage = "Interval not reach"
            TAGString &= "\n" & "[WNCH]" + WinchMessage
            ' Call WriteTagString
            ExitSub
        EndIf
        
        Call CheckWinchStatus

        If WinchStatus = "Standby" Then

            WhereAmI = "Starting A Winch"

            Call WinchConditionsCheck

            If WinchMissingCondition = 0 Then
                Call StartWinchMission
            EndIf  
        EndIf
        
        If NOT Start_Winch_Override Then
            ' Skipped if Start_Winch_Override is True.
            TAGString &= "\n" & "[WNCH]" + WinchMessage
            ' Call WriteTagString
        EndIf
    EndIf
EndSub

' ####################### Collecting Data ############################## 

Sub InitWinchFile
    WinchFileName = Buoy_Name &"_"& Replace(Winch_Date, "-", "") &"_"& Replace(Winch_Time, ":", "")
    WinchFile = FileOpen("CRD:WDATA_"&WinchFileName&".txt","a",-1)

    FileWrite(WinchFile,Winch_Time&" "&Winch_Date&CHR(10),0) ' Not Date with Slash anymore but with hyphen. Fixme
    FileWrite(WinchFile,Buoy_Name&CHR(10),0)     
    FileWrite(WinchFile,Winch_Latitude&","&Winch_Longitude&CHR(10),0)    
    FileWrite(WinchFile,"D"&CHR(10),0)
EndSub


Sub DownloadWinchData
    SerialOpen(SerialWinch,19200,3,20,100) 'Should it be started earlier ??
    SerialFlush(SerialWinch)
    SerialOut(SerialWinch,"D"&CHR(13)&CHR(10),0,1,0)
    
    Do 
        WinchString = ""
        SerialIn(WinchString,SerialWinch,300,10,60)
        FileWrite(WinchFile,WinchString,0)
    Loop While WinchString <> ""
    
    SerialClose(SerialWinch)
    WinchString = ""
EndSub


Sub CollectWinch
    Call CheckWinchStatus
    
    If WinchStatus = "Completed" Then
        WinchStatus = "Standby"
        
        WhereAmI = "Collecting the winch"
        
        Call InitWinchFile
        
        Call DownloadWinchData

        FileClose(WinchFile)
        
        WhereAmI = "Out of Do.. While Loop after winch"        
    EndIf
EndSub
