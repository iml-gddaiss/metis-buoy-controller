'Suna related code
'### Serial Port ###
' Const SerialSUNA = 41 'RS232


'### Data Variables ###
Dim RawSUNADark As String * 80
Dim RawSUNALight As String * 80

Dim SUNADark(8) As Float : Alias SUNADark = SUNA_Dark_Date, SUNA_Dark_Time, Dark_Nitrate, Dark_Nitrogen_in_Nitrate, Dark_254nm, Dark_300nm, Dark_Bromide, SUNA_Dark_RMSE
Dim SUNALight(8) As Float : Alias SUNALight = SUNA_Light_Date, SUNA_Light_Time, Light_Nitrate, Light_Nitrogen_in_Nitrate, Light_254nm, Light_300nm, Light_Bromide, SUNA_Light_RMSE

Dim UpdateSUNATable As Boolean = false

'### Units ###
Units Dark_Nitrate = µM
Units Light_Nitrate = µM
Units Dark_Nitrogen_in_Nitrate = mgN/L
Units Light_Nitrogen_in_Nitrate = mgN/L
Units Dark_Bromide = mg/L
Units Light_Bromide = mg/L


'### Tables ###
DataTable(FileSUNA_Dark,UpdateSUNATable,-1)
  DataInterval(0,0,Sec,10)
  Sample (1,Dark_Nitrate,IEEE4)
  Sample (1,Dark_Nitrogen_in_Nitrate,IEEE4)
  Sample (1,Dark_254nm,IEEE4)
  Sample (1,Dark_300nm,IEEE4)
  Sample (1,Dark_Bromide,IEEE4)
 EndTable
 
DataTable(FileSUNA_Light,UpdateSUNATable,-1)
  DataInterval(0,0,Sec,10)
  Sample (1,Light_Nitrate,IEEE4)
  Sample (1,Light_Nitrogen_in_Nitrate,IEEE4)
  Sample (1,Light_254nm,IEEE4)
  Sample (1,Light_300nm,IEEE4)
  Sample (1,Light_Bromide,IEEE4)
 EndTable


Function SUNATagString
  SunaTAGString = "[SUNA]" & Dark_Nitrate & "," & Light_Nitrate & "," & Dark_Nitrogen_in_Nitrate & "," & Light_Nitrogen_in_Nitrate & "," & Dark_Bromide & "," & Light_Bromide
EndFunction

'### Sampling ###
Sub StartSUNA
  If With_SUNA Then
    WhereAmI = "Starting SUNA"
    SerialOpen(SerialSUNA,19200,3,20,250)
    SerialOut(SerialSUNA,"$","CMD?",1,400)
    SerialOut(SerialSUNA,"measure 1"&CHR(13),CHR(10),1,50)
    SerialIn(RawSUNADark,SerialSUNA,150,CHR(13),80) 'The Dark sample is nearly auto, then at least 12sec for the Light one
      
    Timer(Suna_Timer,Sec,2) 'Timer #5 Start for at least 12 sec
  EndIf
EndSub


Sub CollectSUNA
  ' &4C43 -> LC &0D0A -> CR LF
  Dim NBR ' Number of Bytes Returned
  If With_SUNA Then
    
    SUNADark() = ""
    SUNALight() = ""

    Call TimerDelay_Sec(Suna_Timer, Suna_Delay_Sec)
    
    WhereAmI = "Collect SUNA"  
    SerialInRecord(SerialSUNA,RawSUNALight,&H4C43,0,&H0D0A,NBR,101)
    SerialOut(SerialSUNA,"sleep"&CHR(13),0,1,0)
    SerialClose(SerialSUNA)
    
    If RawSUNADark = "" Then
      RawSUNADark = "NAN"
    EndIf
    
    If RawSUNADark <> "NAN" Then
      SplitStr(SUNADark(),RawSUNADark,",",8,4)
    EndIf
    
    If RawSUNALight <> "NAN" Then
      RawSUNALight = "SATSLC"&RawSUNALight
      SplitStr(SUNALight(),RawSUNALight,",",8,4)
    EndIf

    UpdateSUNATable = true

    TAGString &= "\n" & SUNATagString()
    ' Call WriteTagString

    WhereAmI = "End SUNA"
  EndIf
EndSub
