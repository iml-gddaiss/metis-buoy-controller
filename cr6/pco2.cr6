'PCO2 related code
'### Serial Port ###
' Const SerialPCO2 = 40 'RS232


'### Data Variables ###
Dim RawPCO2 As String * 1000
Dim RawPCO2_Air As String * 100
Dim RawPCO2_Water As String * 100

Public PCO2_Last_Sampling As Long
Public PCO2IsSampling As Boolean

Dim PCO2WStart
Dim PCO2WEnd
Dim PCO2AStart
Dim PCO2AEnd


Dim PCO2AirIsCollected = false
Dim PCO2WaterIsCollected = false


Dim PCO2(14) As String : Alias PCO2 = PCO2_Year, PCO2_Month, PCO2_Day, PCO2_Hour, PCO2_Minute, PCO2_Second, PCO2_Zero, PCO2_AD, CO2, Ave_IRGA, PCO2_Humidity, PCO2_Temp, PCO2_Gaz_Pressure, PCO2_Voltage
Dim CO2_Water = "#.#", PCO2_Gaz_Pressure_Water, PCO2W_Humidity, CO2_Air = "#.#", PCO2_Gaz_Pressure_Air
Dim PCO2_Date As String, PCO2_Time As String, PCO2_Type As String


'### Units ###
Units CO2_Water = ppm
Units CO2_Air = ppm
Units PCO2_Gaz_Pressure_Water = mbar
Units PCO2_Gaz_Pressure_Air = mbar
Units PCO2W_Humidity = %


'### Tables ###
DataTable(FilePCO2W,1,-1)
  DataInterval(0,0,Sec,10)
  Sample (1,PCO2_Type,String)
  Sample (1,PCO2_Date,String)
  Sample (1,PCO2_Time,String)
  Sample (1,PCO2_Zero,IEEE4)
  Sample (1,PCO2_AD,IEEE4)
  Sample (1,CO2_Water,String)
  Sample (1,Ave_IRGA,IEEE4)
  Sample (1,PCO2_Humidity,IEEE4)
  Sample (1,PCO2_Temp,IEEE4)
  Sample (1,PCO2_Gaz_Pressure,IEEE4)
  Sample (1,PCO2_Voltage,IEEE4)
  Sample (1,RawPCO2,String)
  Sample (1,RawPCO2_Water,String)
EndTable

DataTable(FilePCO2A,1,-1)
  DataInterval(0,0,Sec,10)
  Sample (1,PCO2_Type,String)
  Sample (1,PCO2_Date,String)
  Sample (1,PCO2_Time,String)
  Sample (1,PCO2_Zero,IEEE4)
  Sample (1,PCO2_AD,IEEE4)
  Sample (1,CO2_Air,String)
  Sample (1,Ave_IRGA,IEEE4)
  Sample (1,PCO2_Humidity,IEEE4)
  Sample (1,PCO2_Temp,IEEE4)
  Sample (1,PCO2_Gaz_Pressure,IEEE4)
  Sample (1,PCO2_Voltage,IEEE4)
  Sample (1,RawPCO2,String)
  Sample (1,RawPCO2_Air,String)
EndTable


Sub UpdatePCO2AirTable
  Scan(1,Sec, 0, 1)
  CallTable FilePCO2A
  NextScan
EndSub


Sub UpdatePCO2WaterTable
  Scan(1,Sec, 0, 1)
  CallTable FilePCO2W
  NextScan
EndSub


Function PCO2TagString As String
  PCO2TagString = "[PCO2]" & CO2_Water & "," & CO2_Air & "," & PCO2_Gaz_Pressure_Water & "," & PCO2_Gaz_Pressure_Air & "," & PCO2_Humidity
EndFunction


'### Sampling ###

Sub StartPCO2
  WhereAmI = "Start PCO2"  

  RawPCO2 = ""
  RawPCO2_Water = ""
  RawPCO2_Air = ""
  
  SerialOpen(SerialPCO2,19200,3,20,500)
  Delay(1,1,Sec)
  SerialFlush(SerialPCO2)
  SerialOut(SerialPCO2,CHR(27),"",2,50)
  Delay (1,500,mSec)
  SerialOut(SerialPCO2,CHR(27),"",1,0)
  Delay (1,500,mSec)
  SerialOut(SerialPCO2,"1","",1,0)
  Delay (1,500,mSec)
  SerialOut(SerialPCO2,"s","",1,0)
  SerialFlush(SerialPCO2)
  
  PCO2IsSampling = true
  PCO2_Last_Sampling = Public.TimeStamp(1)
  PCO2WaterIsCollected = false
  PCO2AirIsCollected = false
EndSub


Sub ClosePC02
  SerialFlush(SerialPCO2)
  SerialClose(SerialPCO2)
    
  PCO2IsSampling = false
EndSub


Sub CollectPCO2Air
  'Search in RawPCO2 where PCO2_Air starts with "A M"
  PCO2AStart = InStr(1,RawPCO2,"A M,",4)
  PCO2AEnd = InStr(PCO2AStart,RawPCO2,CHR(10),2)
  If PCO2AStart <> 0 AND PCO2AEnd <> 0 Then
    'Collect from RawPCO2 to get RawPCO2_Water
    RawPCO2_Air = Mid(RawPCO2,PCO2AStart,PCO2AEnd - PCO2AStart)
  EndIf

  PCO2AirIsCollected = true
EndSub


Sub CollectPCO2Water
  'Search in RawPCO2 where PCO2_Water starts with "W M"
  PCO2WStart = InStr(1,RawPCO2,"W M,",4)
  PCO2WEnd = InStr(PCO2WStart,RawPCO2,CHR(10),2)
  If PCO2WStart <> 0 AND PCO2WEnd <> 0 Then
    'Collect from RawPCO2 to get RawPCO2_Water
    RawPCO2_Water = Mid(RawPCO2,PCO2WStart,PCO2WEnd - PCO2WStart)
  EndIf

  PCO2WaterIsCollected = true
EndSub


Sub WritePCO2Air
  SplitStr(PCO2,RawPCO2_Air,",",14,0)
  PCO2_Type = "A M"
  PCO2_Date = PCO2(1)&"-"&PCO2(2)&"-"&PCO2(3)
  PCO2_Time = PCO2(4)&":"&PCO2(5)&":"&PCO2(6)
  CO2_Air = PCO2(9)
  PCO2_Gaz_Pressure_Air = PCO2(11)
  Call UpdatePCO2AirTable
  CO2_Air = "#.#"
EndSub


Sub WritePCO2Water
  SplitStr(PCO2,RawPCO2_Water,",",14,0)
  PCO2_Type = "W M"
  PCO2_Date = PCO2(1)&"-"&PCO2(2)&"-"&PCO2(3)
  PCO2_Time = PCO2(4)&":"&PCO2(5)&":"&PCO2(6)
  CO2_Water = PCO2(9)
  PCO2_Gaz_Pressure_Water = PCO2(13)
  PCO2W_Humidity = PCO2(11)
  Call UpdatePCO2WaterTable
  CO2_Water = "#.#"
EndSub


Sub CollectPCO2
  SerialIn(RawPCO2,SerialPCO2,10,"Sample Complete",500)
  
  If NOT PCO2WaterIsCollected Then
    Call CollectPCO2Water
  EndIf

  If NOT PCO2AirIsCollected Then
    Call CollectPCO2Air
  EndIf
    
  If PCO2WaterIsCollected AND PCO2AirIsCollected Then
    
    Call WritePCO2Water
    Call WritePCO2Air

    RawPCO2 = ""
    
    TAGString = PCO2TagString()
    Call WriteTagString

    Call ClosePC02
  EndIf
EndSub


Sub CollectPartialPCO2
  If PCO2AirIsCollected Then
    Call WritePCO2Air
    RawPCO2_Air = ""
  Else
    Call UpdatePCO2AirTable
  EndIf

  If PCO2WaterIsCollected Then
    Call WritePCO2Water
    RawPCO2_Water = ""
  Else
    Call UpdatePCO2WaterTable
  EndIf

  Call ClosePC02
EndSub

Sub SamplePCO2 'Move to pco2.cr6 ? TODO
  Dim time_stamp
  
  If With_PCO2 AND (SBE37_In_Saltwater OR Bypass_Salinity) Then 'Security for PCO2 pump if not in water  FIXME SHOULD ONLY USED FOR STARTING ???
    WhereAmI = "Entering PCO2 Loop"
    time_stamp = Public.TimeStamp(1)
    
    If PCO2IsSampling Then
      If (PCO2_Last_Sampling + 3600) < time_stamp Then 'Watchdog if there's no data in 1Hour (if power disconnected during sampling) # FIXME MAKE A TIMER | Put it in another slow sequence
        ' Call EmptyPCO2Table
        Call CollectPartialPCO2
      Else
        Call CollectPCO2
      EndIf
    
    ElseIf (PCO2_Last_Sampling + (PCO2_Interval_Hr*3600) - 60 <= time_stamp) Then 'minus 60sec to not skip a scan planned at PCO2_Interval hour (got 3599s and didn't sampled)
      Call StartPCO2
    EndIf
  EndIf
EndSub

Sub ResetPCO2RawStrings
  If RawPCO2_Air <> "" AND RawPCO2_Water <> "" Then
    RawPCO2_Air = ""
    RawPCO2_Water = ""
  EndIf
EndSub
