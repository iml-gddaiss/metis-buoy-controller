'compass related code
'### Serial Port ###


'### Data Variables ###

Dim RawCompass(9) As String
Public Compass(3) As Float = {"", "", ""} : Alias Compass = Heading, Pitch, Roll
Units Heading = °
Units Pitch = °
Units Roll = °

Dim UpdateCompassTable As Boolean = false

'### Units ###


'### Tables ###
DataTable (FileCompass,UpdateCompassTable,-1)
  DataInterval(0,0,Sec,10)
  Sample (1,Heading,String)
  Sample (1,Pitch,String)
  Sample (1,Roll,String)
EndTable


Function HexaSign(RawSign) as String * 1
  Select Case RawSign
    Case 10
      HexaSign = "+"
    Case Else
      HexaSign = "-" 
  EndSelect
EndFunction


Sub CollectCompass
  ' Roll and Pitch are not circularly average.
  ' &h -> SA &0D0A -> CR LF
  Dim NBR ' Number of Bytes Returned  
  Dim X, Y
  Dim ASCIIString As String * 100
  Dim HeadingBuff As Float , Heading_x As Float , Heading_y As Float 

  SerialOut(SerialTower,"h"&CHR(04)&CHR(01)&CHR(04)&CHR(09),"h",3,50) ' What is this for ? FIXME
  
  For Y = 1 To Compass_Sample_Size Step 1
    SerialOut(SerialTower,"h"&CHR(04)&CHR(01)&CHR(04)&CHR(09),"h",1,50)  ' Command = h^D^A^D^I (68 04 01 04 09)
    SerialInRecord(SerialTower,ASCIIString,&H68,13,0,NBR,101)   ' First byte sent is address 68
    
    If ASCIIString = "NAN" Then
      Pitch = "NAN"
      Roll = "NAN"
      Heading = "NAN" 
      ExitSub
    EndIf        

    For X = 4 To 12                                      ' Setting each HEX byte in RawCompass Array
      RawCompass(X-3) = Hex(ASCII(ASCIIString(1,1,X)))   ' Received message looks like 68 0D 01 84 00 11 22 10 33 44 05 66 77 99
    Next X                                               '                             ID LG AD MS (- Roll) (+Pitch) ( Head ) CHK
            
    If Len(RawCompass(8)) = 1 Then
      RawCompass(8) = "0" + RawCompass(8)
    EndIf

    'Array are in order of Roll, Pitch & Heading
    Roll = Roll + CTYPE(HexaSign(RawCompass(1)) & RawCompass(2) & "." & RawCompass(3), Float)
    Pitch = Pitch + CTYPE(HexaSign(RawCompass(4)) & RawCompass(5) & "." & RawCompass(6), Float)
    HeadingBuff = CTYPE(RawCompass(7) & RawCompass(8) & "." & RawCompass(9), Float)
    Heading_x = Heading_x + COS(HeadingBuff)
    Heading_y = Heading_y + SIN(HeadingBuff)
    Delay(1,400,mSec)
    
  Next
  
  ' Does Roll and Pitches need to be circularly averaged.
  Roll = Round(Roll/Compass_Sample_Size,2) ' Goes from -85 to 85 so this should be fine.
  Pitch = Round(Pitch/Compass_Sample_Size,2) ' Goes from -85 to 85 so this should be fine.
  Heading = ATN2(Heading_y, Heading_x)
EndSub

Sub ApplyMagneticCorrection
  Heading = Round((Heading + CTYPE(GPS_Mag_Var, Float)) MOD 360, 2)
EndSub


Sub SampleCompass
  Compass() = ""
  If With_Compass Then
    Call logging("Sampling Compass")
    WhereAmI = "Sample Compass"   
    
    Call TimerDelay_Sec(Tower_Timer, Compass_Delay_Sec)
        
    Call CollectCompass

    Call ApplyMagneticCorrection

    UpdateCompassTable = true
    
    WhereAmI = "End Compass"
    Call logging("Compass Sampled")
  EndIf
EndSub
