'OCR related code
'### Serial Port ###

' Const SerialOCRI = 34
' Const SerialOCRR = 39 'RS232



'### Data Variables ###
Public OCRR_String As String * 1000
Public OCRI_String As String * 1000

Dim UpdateHyperOCRTable As Boolean = false


'### Units ###
'### Tables ###
DataTable(FileHyperOCR,UpdateHyperOCRTable,-1)
  DataInterval(0,0,Sec,10)
  Sample(1,OCRR_String,String)
  Sample(1,OCRI_String,String)
EndTable

Function CollectOCR(OCR_Type As String) As String * 1000
  Dim Y, X
  
  Dim RawHex As String * 500
  Dim NBR ' Number of Bytes Returned
  
  Dim String_Buffer As String
  Dim OCR_String As String * 1000

  OCR_String = ""

        
  For Y = 1 To 3
    If OCR_Type = "R" Then
      SerialInRecord(SerialOCRR,RawHex,&H5341,0,&H0D0A,NBR,101)
    Else
      SerialInRecord(SerialOCRI,RawHex,&H5341,0,&H0D0A,NBR,101)
    EndIf

    String_Buffer = Hex(ASCII(RawHex(1,1,1)))
    
    If String_Buffer = "54" Then
      For X = 1 To 393
        String_Buffer = Hex(ASCII(RawHex(1,1,X)))
        If Len(String_Buffer) = 1 Then
          String_Buffer = "0" + String_Buffer
        EndIf
        OCR_String = OCR_String + String_Buffer
      Next X      
      OCR_String = "5341" + OCR_String + "0D0A"
      ExitFor
    EndIf
    Delay(1,2,Sec)
  Next Y

  Return OCR_String

EndFunction


Sub SampleOCRs
  
  If With_OCR_R AND WellPump_IsPowered Then
    Call logging("Sampling OCR R")
    WhereAmI = "Sample OCR R"
    
    SerialOpen(SerialOCRR,19200,3,0,800)
    Call TimerDelay_Sec(Pump_Timer, OCR_R_Delay_Sec)
    SerialFlush(SerialOCRR)
  
    OCRR_String = CollectOCR("R")

    If OCRR_String = "" Then
      Call logging("OCR R string was NOT received.")
      Call Failed_Sampling_Email("OCR-R")
    Else 
      Call logging("OCR R string was received.")
    EndIf
    
    SerialClose(SerialOCRR)
    
    UpdateHyperOCRTable = true
    WhereAmI = "End OCRR"
    Call logging("OCR R Sampling Done")
  EndIf
  
  If With_OCR_I AND Tower_IsPowered Then
    Call logging("Sampling OCR I")
    WhereAmI = "Sample OCR I"

    SerialOpen(SerialOCRI,19200,51,0,800)
    Call TimerDelay_Sec(Tower_Timer, OCR_I_Delay_Sec)
    SerialFlush(SerialOCRI)

    OCRI_String = CollectOCR("I")

    If OCRI_String = "" Then
      Call logging("OCR I string was NOT received.")
      Call Failed_Sampling_Email("OCR-I")
    Else 
      Call logging("OCR I string was received.")
    EndIf
    
    SerialClose(SerialOCRI)
    
    UpdateHyperOCRTable = true
    WhereAmI = "End OCRI"
    Call logging("Sampling OCR I")
  EndIf
  
EndSub
