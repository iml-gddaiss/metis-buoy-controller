' GPS related code
'### Serial Port ###
' Const SerialGPS = 16 'U11-U12 (trouble with GPS on SDM)


'### Data Variables ###
Dim RawGPS As String * 100
Dim HVS(13) As String : Alias HVS = GPS_Header, GPS_Time, GPS_Status, Lat, Lat_Hem, Lon, Lon_Hem, Speed_Over_Ground, Course_Over_Ground, GPS_Date, Magnetic_Variation,Mag_Var_Dir, Check_GPS

' Dim GPS_SOG_X As Float, GPS_SOG_Y As Float Changed to below
Public GPS_SOG_X As Float, GPS_SOG_Y As Float

Public Latitude As String,Longitude As String, SOG As Float, COG As Float, Mag_Var As Float

Dim UTC_Date_Hyphen As String, UTC_Date_Slash As String, UTC_Date_Short As String, UTC_Time_Short As String, UTC_Time_DDot As String


'### Units ###
Units SOG = Kn
Units COG = 째
Units Mag_Var = 째


'### Tables ###
DataTable(FileGPS,1,-1)
    DataInterval(0,0,Sec,10)
    Sample(1,UTC_Date_Slash,String)
    Sample(1,UTC_Time_DDot,String)
    Sample(1,Latitude,String)
    Sample(1,Longitude,String)  
    Sample(1,SOG,IEEE4)
    Sample(1,COG,IEEE4)
    Sample(1,Mag_Var,IEEE4)
EndTable

Sub GPSTable
  Scan(1,Sec, 0, 1)
  CallTable FileGPS
  NextScan
EndSub


'### Sampling ###
Sub SampleGPS
    Dim X
    
    If With_GPS <> 0 Then
        WhereAmI = "Sample GPS"
        
        SerialOpen(SerialGPS,19200,3,20,1000)
        SerialFlush(SerialGPS)
        SerialIn(RawGPS,SerialGPS,150,10,80)
        HVS() = 0 'HVS = GPS_Header, GPS_Time, GPS_Status, Lat, Lat_Hem, Lon, Lon_Hem, Speed_Over_Ground, Course_Over_Ground, GPS_Date, Magnetic_Variation, Mag_Var_Dir, Check_GPS
        GPS_SOG_X = 0
        GPS_SOG_Y = 0
        SOG = 0
        COG = 0
        For X = 1 To 10 Step 1
            SerialIn(RawGPS,SerialGPS,150,10,80)
            SplitStr(HVS(),RawGPS,",",13,5)
            If Speed_Over_Ground = "" Then
                COG = 0
                SOG = 0
            Else
                COG = Course_Over_Ground
                SOG = Speed_Over_Ground
            EndIf
            
            GPS_SOG_X = GPS_SOG_X + (SOG * COS(COG))
            GPS_SOG_Y = GPS_SOG_Y + (SOG * SIN(COG))
        Next
        
        SerialClose(SerialGPS)
        
        'Putting the CR6 at GPS Date/Time
        UTC_Time_DDot = Mid(GPS_Time,1,2)&":"&Mid(GPS_Time,3,2)&":"&Mid(GPS_Time,5,2)
        UTC_Date_Hyphen = "20"&Mid(GPS_Date,5,2)&"-"&Mid(GPS_Date,3,2)&"-"&Mid(GPS_Date,1,2)
        ClockSet (UTC_Date_Hyphen&" "&UTC_Time_DDOt)
        
        'Creating variables with different formats
        UTC_Time_Short = Mid(ScanDateTime,12,2)&Mid(ScanDateTime,15,2)&Mid(ScanDateTime,18,2)
        UTC_Time_DDot = Mid(ScanDateTime,12,8)
        
        UTC_Date_Slash = Mid(ScanDateTime,1,4)&"/"&Mid(ScanDateTime,6,2)&"/"&Mid(ScanDateTime,9,2)
        UTC_Date_Hyphen = Mid(ScanDateTime,1,10)
        UTC_Date_Short = Mid(ScanDateTime,1,4)&Mid(ScanDateTime,6,2)&Mid(ScanDateTime,9,2)
        
        'Processing COG and SOG
        SOG = Round(SQR(PWR(GPS_SOG_X,2)+PWR(GPS_SOG_Y,2))/10,2)
        COG = Round(ATN2(GPS_SOG_Y,GPS_SOG_X),1)
        If COG < 0 Then
            COG = 360 + COG
        EndIf        

        'Processing Lat/Long & Magnetic Variation                  
        Latitude = Mid(Lat,1,2)&"째"&Mid(Lat,3,6)&"'"&Lat_Hem
        Longitude = Mid(Lon,1,3)&"째"&Mid(Lon,4,6)&"'"&Lon_Hem

        ' Converting Magnetic Variation to decimal
        If Mid(Mag_Var_Dir,1,1) = "W" Then
            Magnetic_Variation = "-"&Mid(Magnetic_Variation,2,5)
        Else
            Magnetic_Variation = "+"&Mid(Magnetic_Variation,2,5)
        EndIf

        'Processing Buoy's Speed&Direction 
        If Speed_Over_Ground = "" Then
            Speed_Over_Ground = 0
        EndIf
        
        If Course_Over_Ground = "" Then
            Course_Over_Ground = 0
        EndIf
        
        'Putting the String value in Float or Long variables 
        Mag_Var = Magnetic_Variation  

        Call GPSTable
        
        WhereAmI = "EndGPS, start Start Tower"
    EndIf
EndSub

'### Processing ###