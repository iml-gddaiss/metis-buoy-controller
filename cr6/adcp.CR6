' adcp related code
'### Serial Port ###
' Const SerialADCP = 12 'U3-U4


'### Data Variables ###
Dim ADCP(ADCP_Num_Bin + 4) As String * 100

Dim ADCPHeader(3) As String : Alias ADCPHeader = ADCPDate, ADCPTime, ADCPSmpNbr
Dim ADCPBin1(11) As Float : Alias ADCPBin1 = Bin, ADCPDir, ADCPMag, EW, NS, Vert, Err, Echo1, Echo2, Echo3, Echo4

Dim UpdateADCPTable As Boolean = false

'### Units ###
'### Tables ###
DataTable(FileADCP,UpdateADCPTable,-1)
  DataInterval(0,0,Sec,10)
  Sample(ADCP_Num_Bin + 4, ADCP,String)
EndTable


Function ADCPTagString As String * 200
  ADCPTagString = "[RDI]" & ADCPDate & "," & ADCPTime & "," & EW & "," & NS & "," & Vert & "," & Err
EndFunction


'### Sampling ###
Sub StartADCP
  If With_ADCP Then  
    Call logging("Starting ADCP")
    SerialOpen(SerialADCP,115200,3,0,3000)
    SerialFlush(SerialADCP)
  EndIf
EndSub
 
Sub CollectADCP
  Dim X, Y

  If With_ADCP Then
    Call logging("Collecting ADCP") 

    ADCP() = ""
    ADCPHeader() = ""
    ADCPBin1() = ""

    For X = 1 To ADCP_Num_Bin + 4 Step 1
      SerialIn(ADCP(X),SerialADCP,10,10,100)
      If X > 3 Then
        For Y = 1 To 4 Step 1 'Removes all single spaces (up to 8 space -> 1)
          ADCP(X) = Replace(ADCP(X),"  "," ")
        Next Y
        ADCP(X) = LTrim(ADCP(X)) 
        ADCP(X) = Replace(ADCP(X)," ",",")
      EndIf
      ADCP(X) = Replace(ADCP(X),CHR(13),"")
      Call logging("Row " & X & ": " & ADCP(X))
    Next X

    SplitStr(ADCPHeader,ADCP(1)," ",3,5)
    SplitStr(ADCPBin1,ADCP(5),",",11,5)

    If ADCP(1) = "" Then ' FIXME test and check if its the correct way to check
      Call Failed_Sampling_Warning("ADCP")
    EndIf

    SerialFlush(SerialADCP)

    UpdateADCPTable = true

    TAGString &= ADCPTagString()
  EndIf

EndSub

Dim adcp_return_message As String * 200

Sub DeployADCP
  Call logging("Entering ADCP Deploy sequence.")
  If 11 <= SerialADCP and SerialADCP <= 16 Then
    SerialBrk(SerialADCP,200)
    Delay(1,1,Sec)
    SerialIn(adcp_return_message,SerialADCP,200,"All Rights Reserved."&CHR(13),120)
    Call logging(adcp_return_message)   
  Else
    Call logging("Cannot send break to ADCP. Invalid Port.")
    ExitSub
  EndIf
  Call logging("CS")
  SerialOut(SerialADCP,"CS"&CHR(13),"",1,100)
  Delay(1,1,Sec)
  SerialIn(adcp_return_message,SerialADCP,200,13,120)
  Call logging(adcp_return_message)   
EndSub

Sub ShutdownADCP
  Call logging("Entering ADCP Shutdown sequence.")
  If 11 <= SerialADCP and SerialADCP <= 16 Then
    SerialBrk(SerialADCP,200)
    Delay(1,1,Sec)
    SerialIn(adcp_return_message,SerialADCP,200,"All Rights Reserved."&CHR(13),120)
    Call logging(adcp_return_message)   
  Else
    Call logging("Cannot send break to ADCP. Invalid Port.")
    ExitSub
  EndIf
  
  Call logging("CK")
  SerialOut(SerialADCP,"CK"&CHR(13),"",1,100)
  Delay(1,1,Sec)
  SerialIn(adcp_return_message,SerialADCP,200,13,120)
  Call logging(adcp_return_message)   
EndSub
