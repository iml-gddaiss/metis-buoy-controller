'Power Controller related code

'### Data Variables ###
Dim RawPower As String * 150
Dim Power(11) As String : Alias Power = VBatt1, ABatt1, VBatt2, ABatt2, VSolar, ASolar, AMain, ATurbine, AWinch, PM_RH, Relay_State

Dim UpdatePowerTable As Boolean = false

'### Units ###
'### Tables ###
DataTable(FilePower,UpdatePowerTable,-1)
  DataInterval(0,0,Sec,10)
  Sample (1,VBatt1,IEEE4)
  Sample (1,ABatt1,IEEE4)
  Sample (1,VBatt2,IEEE4)
  Sample (1,ABatt2,IEEE4)
  Sample (1,VSolar,IEEE4)
  Sample (1,ASolar,IEEE4)
  Sample (1,AMain,IEEE4)
  Sample (1,ATurbine,IEEE4)
  Sample (1,AWinch,IEEE4)
  Sample (1,PM_RH,IEEE4)
  Sample (1,Relay_State,String)
 EndTable

'### Sampling ###

Function PowerTagString as String * 200
  PowerTagString = "[POWR]" & VBatt1 & "," & ABatt1 & "," & VBatt2 & "," & ABatt2 & "," & VSolar & "," & ASolar & "," & AMain & "," & ATurbine & "," & AWinch & "," & PM_RH & "," & Relay_State
EndFunction

Sub CollectPowerController
  Dim X

  RawPower = ""
  Power() = ""

  For X = 1 To 4
    SerialOut(SerialPower,CHR(13),">",2,50)
    SerialOut(SerialPower,"d",0,1,50)
    SerialOut(SerialPower,"v",0,1,50)
    SerialOut(SerialPower,CHR(13),CHR(10),1,50)  ' FIXME TEST
    SerialIn(RawPower,SerialPower,100,13,200)
    SplitStr(Power,RawPower,0,11,0)

    Call logging("Raw Power Controller (attempt "&X&"): " & RawPower)
    
    If Len(Relay_State) > 5 Then 
      ExitFor
    EndIf
  Next X
  
  If RawPower = "" Then
    Call Failed_Sampling_Email("Power Controller")
  EndIf
EndSub


Sub SamplePowerController
  
  If With_Power Then
    Call logging("Sampling Power Controller")
    WhereAmI = "Sample Power Controller"
      
    SerialOpen(SerialPower,19200,51,600,200) 'No Flush FIXME 
      
    Call CollectPowerController  
    
    SerialClose(SerialPower)
    
    UpdatePowerTable = true

    TAGString &= PowerTagString()

    Call Low_Battery_Check

    Call logging("Power Controller Done")
  EndIf
EndSub


Sub Low_Battery_Check
  If VBatt1 < Battery_Low_Voltage_Threshold _ 
     OR VBatt2 < Battery_Low_Voltage_Threshold Then
    Call logging("[Warning] Battery Low")
    Call Battery_Low_Email
  EndIf

EndSub