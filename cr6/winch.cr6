'Winch related code
'### Serial Port ###
Const SerialWinch = 13 'U5-U6


'### Data Variables ###
Const Min_Air_Temp_for_Winch = 1
Const Max_Wave_H13_for_Winch = 2
Const Min_Wave_Period_for_Winch = 6
Const Max_Flow_for_Winch = 1
Const Max_Buoy_Speed_for_Winch = 1
Const Min_Voltage_for_Winch = 12.2

Public OnWhichBattery ' On?Winch?Battery
Public WinchMessage As String * 50
Public MissingCondition As Long
Public Winch_Voltage As Float
Public Winch_Last_Sample As Long
Public Winch_Sampling As Boolean
Public WinchString As String * 50
Public WinchFileName As String * 50

Dim Winch_Date_Hyphen As String
Dim Winch_Date_Slash As String
Dim Winch_Time_Short As String
Dim Winch_Time_DDot As String
Dim Winch_Lat As String
Dim Winch_Long As String


'### Units ###
'### Tables ###
'### Sampling ###


Sub CheckWinchAir(Air_Temp)
    If Air_Temp < Min_Air_Temp_for_Winch Then
        MissingCondition = MissingCondition + 1
        WinchMessage = "The air temperature is too cold"
    EndIf
EndSub

Sub CheckWinchWave(Wave_H13, Wave_Period)
    If Wave_H13 > Max_Wave_H13_for_Winch Then
        MissingCondition = MissingCondition + 1
        WinchMessage = "Average wave is too high"
    EndIf
        
    If Wave_H13 > 1 AND Wave_Period < Min_Wave_Period_for_Winch  AND Wave_Period <> 0 Then
        MissingCondition = MissingCondition + 1
        WinchMessage = "Wave period is too short"
    EndIf
EndSub


Sub CheckWinchGPS(SOG)
    If SOG > Max_Buoy_Speed_for_Winch Then
        MissingCondition = MissingCondition + 1
        WinchMessage = "The buoy is moving too fast"
    EndIf
EndSub

Sub CheckWinchPower(VBatt1, VBatt2)
    OnWhichBattery = Mid(PowerString(11),4,1)
    If OnWhichBattery = 1 Then
        Winch_Voltage = VBatt1
    Else If OnWhichBattery = 2 Then
        Winch_Voltage = VBatt2
    EndIf
    
    If Winch_Voltage < Min_Voltage_for_Winch Then
        MissingCondition = MissingCondition + 1
        WinchMessage = "The Winch voltage is too low"
    EndIf
EndSub


Sub StartWinch(Depth_for_Winch, Buoy_TimeStamp_1990, GPS_Date, UTC_Date_Hyphen, UTC_Time_Short, UTC_Time_DDot, Lat, Lon, Lat_Hem, Lon_Hem)
    SerialOpen(SerialWinch,19200,3,20,100)
    WinchString = ""
    SerialFlush(SerialWinch)
    Delay(1,500,mSec)
    SerialIn(WinchString,SerialWinch,100,10,60)
    
    If WinchString = "" Then
        SerialOut(SerialWinch,CHR(13),"",1,0)
        SerialOut(SerialWinch,"p1"&CHR(13)&CHR(10),.,2,300)
        Delay(1,2,Sec)
        SerialFlush(SerialWinch)
        SerialIn(WinchString,SerialWinch,100,10,100)
        SerialOut(SerialWinch,"p0"&CHR(13)&CHR(10),.,2,300)
        If Mid(WinchString,1,3) = "[S]" Then 'If String received from ctd
            SerialOut(SerialWinch,"X"&CHR(13)&CHR(10),"[W]1B>Operation Complete",2,200)
            SerialOut(SerialWinch,"R"&CHR(13)&CHR(10),"[W]09>Top Switch Activated",3,1000)
            SerialOut(SerialWinch,"K"&CHR(13)&CHR(10),"[W]13>Lenght Manually Cleared",2,200)
            SerialOut(SerialWinch,"Y"&Depth_for_Winch&CHR(13)&CHR(10),CHR(13),1,0)
            WinchMessage = "Winch Mission Started"
            Winch_Sampling = True
            Winch_Last_Sample = Buoy_TimeStamp_1990
            Winch_Date_Slash = Mid(GPS_Date,5,2)&"/"&Mid(GPS_Date,3,2)&"/"&Mid(GPS_Date,1,2)
            Winch_Date_Hyphen = UTC_Date_Hyphen
            Winch_Time_Short = UTC_Time_Short
            Winch_Time_DDot = UTC_Time_DDot
            Winch_Lat = Mid(Lat,1,2)&" "&Mid(Lat,3,5)&Lat_Hem
            Winch_Long = Mid(Lon,1,3)&" "&Mid(Lon,4,5)&Lon_Hem
        Else 'No string from CTD
            WinchMessage = "No String received from CTD"
        EndIf
    Else
        WinchMessage = "Winch already in function"
        Winch_Sampling = True
    EndIf
EndSub


Sub CheckWinch(WhereAmI)      
    SerialOpen(SerialWinch,19200,3,20,100)
    SerialFlush(SerialWinch)
    SerialIn(WinchString,SerialWinch,200,10,100)
    If WinchString = "" Then
        WinchMessage = "Winch Complete"
    Else
        WinchMessage = "Winch in Progress"
    EndIf
EndSub

Sub CollectWinch(WhereAmI, Buoy_Name)      
    If WinchMessage == "Winch Complete" Then
        Winch_Sampling = False
        
        WhereAmI = "Collecting the winch"
            WinchFileName = Buoy_Name&"_"&Winch_Date_Hyphen&"_"&Winch_Time_Short
            WinchFile = FileOpen("CRD:WDATA_"&WinchFileName&".txt","a",-1)
            FileWrite(WinchFile,Winch_Time_DDot&" "&Winch_Date_Slash&CHR(10),0)
            FileWrite(WinchFile,Buoy_Name&CHR(10),0)     
            FileWrite(WinchFile,Winch_Lat&","&Winch_Long&CHR(10),0)    
            FileWrite(WinchFile,"D"&CHR(10),0)

        SerialOut(SerialWinch,"D"&CHR(13)&CHR(10),0,1,0)
        Do 
            WinchString = ""
            SerialIn(WinchString,SerialWinch,300,10,60)
            FileWrite(WinchFile,WinchString,0)
        Loop While WinchString <> ""
        
        
        WhereAmI = "Out of Do.. While Loop after winch"
        WinchString = ""
        FileClose(WinchFile)
    EndIf
EndSub

'### Processing ###