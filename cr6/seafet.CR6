'SEAFET related code

'### Data Variables ###
Public RawSeafet As String * 120

Public Seafet_Value(11) As String : Alias Seafet_Value = SeaFet_Serial, Seafet_Date_Time, Sample_Number, Error_Flag, Ext_pH, Int_pH, Volt_Ext_pH, Volt_Int_pH, pH_Temp, Seafet_Rel_Hum, Seafet_Int_Temp
Public Seafet_pH_Calc(2) As Float = {"", ""} : Alias Seafet_pH_Calc = Ext_pH_Calc, Int_pH_Calc

Public Seafet_SerialNumber As String * 15 ' Should be Length 11.

'Constant to auto-proceed the pH value with Temp and Sal from CTD
Const GasConstantR = 8.3144621 'Universal Gas constant in J/Kmol
Const FaradayConstant = 96485.365 'Faraday constant
Const KelvinToCelsius = 273.15

Public Seafet_Coeff(4) As Float = {"", "", "", ""} : Alias Seafet_Coeff = K0_Int, K2_Int, K0_Ext, K2_Ext

Dim UpdateSeafetTable As Boolean = false

DataTable (FileSeafet,UpdateSeafetTable,-1)
  DataInterval (0,0,Sec,10)
  Sample (1,Ext_pH_Calc,IEEE4).  ' Pourquoi il y a un point -------------------------
  Sample (1,Int_pH_Calc,IEEE4)
  Sample (1,SBE37.Temperature,IEEE4)
  Sample (1,SBE37.Salinity,IEEE4)
  Sample (1,K0_Ext,IEEE4)
  Sample (1,K2_Ext,IEEE4)
  Sample (1,K0_Int,IEEE4)
  Sample (1,K2_Int,IEEE4)
  Sample (1,Ext_pH,IEEE4)
  Sample (1,Int_pH,IEEE4)
  Sample (1,Error_Flag,FP2)
  Sample (1,Volt_Ext_pH,IEEE4)
  Sample (1,Volt_Int_pH,IEEE4)
  Sample (1,pH_Temp,IEEE4)
EndTable


Function PHTagString as String
  PHTagString = "[PH]" & Ext_pH_Calc & "," & Int_pH_Calc & "," & Error_Flag & "," & Ext_pH & "," & Int_pH
EndFunction

Sub StartSeafet
  Call logging("Starting SeaFet")
  SerialOpen(SerialSeafet,19200,3,20,900) 'Seafet string is around 100, but bigger buffer for Coef.
EndSub


Sub CollectSeafet
  Call logging("Collecting SeaFet")
  Dim X

  Seafet_Value() = ""

  For X = 1 To 3 Step 1
    SerialFlush(SerialSeafet)
    SerialOut(SerialSeafet,CHR(13),"S>",2,50)
    
    If X = 1 Then
      SerialOut(SerialSeafet,"tss"&CHR(13),CHR(13),1,50)
    Else
      SerialOut(SerialSeafet,"sl"&CHR(13),CHR(13),1,50)
    EndIf
    
    SerialIn(RawSeafet,SerialSeafet,800,CHR(13),125) 'Seafet string is around 100
    Call logging("Raw Seafet: " & RawSeafet)
    SplitStr(Seafet_Value,RawSeafet,",",11,15)

    If Len(RawSeafet) > 97 AND Mid(RawSeafet,24,1) = "T" AND CTYPE(Int_pH,Float) < 12 AND CTYPE(Int_pH,Float) > 3 AND CTYPE(Ext_pH,Float) < 12 AND CTYPE(Ext_pH,Float) > 3 Then
      ExitFor
    EndIf
  
  Next X

  If Error_Flag = "0000" Then
    Error_Flag = 0
  Else
    Error_Flag = 1
  EndIf

EndSub


Function Fetch_Seafet_Coefficient(coeff_tag As String) As String
  Dim _coeff_k_ As String
  SerialFlush(SerialSeafet)
  SerialOut(SerialSeafet,CHR(13),"S>",2,50)
  SerialOut(SerialSeafet,coeff_tag&CHR(13),"",1,0)
  SerialIn(_coeff_k_,SerialSeafet,100,39,40)
  SerialIn(_coeff_k_,SerialSeafet,100,39,20)
  
  If Len(_coeff_k_) <> 13 OR Mid(_coeff_k_,10,1) <> "e" Then
    _coeff_k_ = ""
  EndIf

  Fetch_Seafet_Coefficient = _coeff_k_
EndFunction


Sub FetchSeafetCoefficents
  Call logging("Collecting SeaFet Coefficient")
  Dim X
  Seafet_Coeff() = ""
  For X = 1 To 5 Step 1 ' Fixme Question: Is this to have 5 attemps -------------------------------------------?
    If K0_Ext = "NAN" Then
      K0_Ext = Fetch_Seafet_Coefficient("K0")
    EndIf
    
    If K2_Ext = "NAN" Then
      K2_Ext = Fetch_Seafet_Coefficient("K2")
    EndIf
    
    If K0_Int = "NAN" Then
      K0_Int = Fetch_Seafet_Coefficient("KDF0")             
    EndIf
    
    If K2_Int = "NAN" Then
      K2_Int = Fetch_Seafet_Coefficient("KDF2")         
    EndIf
  Next X 
  Call logging("SeaFet Coefficient. K0 Ext: " & K0_Ext & ", K2 Ext: " & K2_Ext & ", K0 Int: " & K0_Int & ", K2 Int: " & K2_INT)          
EndSub

Function LOG_10(X as float) As Float
  LOG_10 = LOG(X) / LOG(10)
EndFunction

Sub SeafetpH_Calc(Temperature As Float, Salinity As Float)
  ' Compute pH Ext and Int from raw Voltage and InSitu temperature and salinity.
  ' From Sea-Bird Scientific documentation.
  '
  '   T : temperature in Kelvin
  '   t : temperature in Celsius
  '   psal : salinity in PSU
  '
  ' ### (Martz et al. 2010) ###
  '   pH_INT = (V_INT - k0_INT - K2_INT * t) / S_nernst
  '   V_INT : raw interior voltage
  '   K0_INT : Interior cell standard potential Offset (From Calibration).
  '   K2_INT : Interior temperature slope coefficient (From Calibration).
  '
  ' ### (Johnson et al. 2016) ###
  '   pH_EXT = (V_EXT - k0_EXT - k2_EXT * t) / S_nernst
  '            + log(Cl_T)
  '            + 2*log(Y_HCl)_T
  '            - log(1 + S_T/K_S)
  '            - log((1000 - 1.005 * psal) / 1000))
  '   V_EXT : raw exterior voltage
  '   K0_EXT : Exterior cell standard potential Offset (From Calibration).
  '   K2_Ext : Exterior temperature slope coefficient (From Calibration).
  '
  ' ### Nernst Equation ###
  '  S_nernst = (R * T * ln(10)) / F 
  '  F : Faraday Constant
  ' 
  ' ### log_of_HCl_activity_as_temperature_function (Khoo et al. 1977) ###
  '                    -A_DH * sqrt(I)
  '   log(Y_HCl)_T = --------------------  + (0.08885 - 0.000111 * t) * I
  '                   1 + 1.394 * sqrt(I)
  ' ### debye_huckel_HCl_activity_constant  (Khoo et al. 1977) ###
  '   A_DH = 0.0000034286 * t**2 + 0.00067524 * t + 0.49172143            
  '
  ' ### total_chloride_in_seawater (Dickson et al. 2007) ###
  '   Cl_T = (0.99889/35.453)*(psal/1.80655)*(1000/(1000-1.005*psal))
  '
  ' ### acid_dissociation_constant_HSO4 (Dickson et al. 2007) ###
  '   K_S = (1 - 0.001005 * S)*exp( a0 + a1 + a2 + a3 + a4)
  '     a0 = (-4276.1/T) + 141.328 - 23.093 * ln(T)
  '     a1 = ((-13856/T) + 324.57 - 47.986*ln(T))*sqrt(I)
  '     a2 = ((35474/T) - 771.54 + 114.723*ln(T))*I
  '     a3 = -(2698/T)*I**(3/2)
  '     a4 =  (1776/T)*I**2
  '
  ' ### sample_ionic_strength (Dickson et al. 2007) ###
  '   I = (19.924*psal)/(1000-1.005*psal)
  '
  ' ### total_sulfate_in_seawater (Dickson et al. 2007)###
  '   S_T = (0.1400 / 96.062) * (psal / 1.80655)
  ' 
  '
  '  References
  '  ----------
  '  .. [1] T.R. Martz, J. G. Connery, K. S. Johnson. Testing the Honeywell Durafet for seawater pH applications.
  '         Limnol. Ocean-ogr.: Methods, 8:172-184, 2010.
  '  .. [2] Johnson et al.2016, Analytical Chemistry, DeepSea DuraFET: A pressure tolerant pH sensor designed for global sensor networks.
  '  .. [3] Khoo et al. 1977, Analytical Chemistry,
  '         Determination of hydrogen ion concentrations in seawater from 5C to 40C: standard potentials at salinities 20 to 45%
  '  .. [4]  Dickson et al. 2007, IOCCP Report No.8, Guide to Best Practices for Ocean CO2 Measurements
  '  .. [5] Sea-Bird Scientific, Technical Note on Calculating pH, Application Note 99
  Call logging("Calculating pH")
  Seafet_pH_Calc() = ""
  
  Dim Snernst, Clt, log_yHClT, St, Temperature_K, I, Adh
  Dim Ks_Tab(6) : Alias Ks_Tab = Ks1, Ks2, Ks3, Ks4, Ks5, Ks
    
  Temperature_K = KelvinToCelsius + Temperature
  
  Snernst = (GasConstantR*Temperature_K*LOG(10))/FaradayConstant                    ' Nernst equation
  Clt = (0.99889/35.453)*(Salinity/1.80655)*(1000/(1000-(1.005*Salinity)))         ' total_chloride_in_seawater
  I = (19.924*Salinity)/(1000-(1.005*Salinity))                                    ' sample_ionic_strength
  Adh = (0.0000034286*(Temperature^2))+(0.00067524*Temperature)+0.49172143           ' debye_huckel_HCl_activity_constant
  log_yHClT = (-Adh*SQR(I))/(1+(1.394*SQR(I)))+((0.08885-(0.000111*Temperature))*I) ' log_of_HCl_activity_as_temperature_function
  St = (0.1400/96.062)*(Salinity/1.80655)                                          ' total_sulfate_in_seawater
  
  ' acid_dissociation_constant_HSO4
  Ks1 = (-4276.1/Temperature_K)+141.328-(23.093*LOG(Temperature_K))
  Ks2 = ((-13856/Temperature_K)+324.57-(47.986*LOG(Temperature_K)))*SQR(I)
  Ks3 = ((35474/Temperature_K)-771.54+(114.723*LOG(Temperature_K)))*I
  Ks4 = -(2698/Temperature_K)*I^1.5
  Ks5 = (1776/Temperature_K)*I^2
  Ks = (1-(0.001005*Salinity))*EXP(Ks1+Ks2+Ks3+Ks4+Ks5)
  
  Int_pH_Calc = Round((CTYPE(Volt_Int_pH,Float) - K0_Int - (K2_Int*Temperature))/Snernst,4)
  Ext_pH_Calc = Round(((CTYPE(Volt_Ext_pH,Float) - K0_Ext - (K2_Ext*Temperature))/Snernst) + LOG_10(Clt) + (2*log_yHClT) - LOG_10(1+(St/Ks)) - LOG_10((1000-(1.005*Salinity))/1000),4)
  
  Call logging("Calculated pH. Ext: " & Ext_pH_Calc & ", Int: " & Int_pH_Calc)
EndSub


Sub SampleSeafet(Temperature As Float, Salinity As Float)
  If With_Seafet Then
    Call logging("Sampling Seafet")
    WhereAmI = "Sample Seafet"
    
    Call StartSeafet
    
    Call CollectSeafet
    
    If Seafet_Auto_Calculation AND SBE37_In_Saltwater Then

      If SeaFet_Serial <> Seafet_SerialNumber Then
        'Fetch the Coefficients only if the serial number doesn't match the one previously recorded.
        Call FetchSeafetCoefficents
        Seafet_SerialNumber = SeaFet_Serial
      EndIf
      ' Calculation is carried out even if the Temperature/Salinity/Seafet_Coeff are Nan. 
      ' In the case, Nan values are returned for the Ph_Calc Values.
      Call SeafetpH_Calc(Temperature, Salinity)    
    EndIf

    UpdateSeafetTable = true
    
    TAGString &= "\n" & PHTagString() 
    ' Call WriteTagString
    WhereAmI = "End Seafet"
    Call logging("Seafet Done")
  EndIf
EndSub
